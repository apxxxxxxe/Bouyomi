// ****使い方****
// 関数名はイベント名そのままです。SHIORI/3.0テンプレートのように、
// 文字列取得時頭にOn_がつくようなことはありません。
// 値を返すとその内容をSakuraScriptとして実行します
// 以下の変数に代入すると、動作を制御できます。
//   res_event , res_reference[0]...[n]
//     指定したイベントをReferenceつきで発生します。referenceは省略可
//   res_target
//     スクリプトやイベントをどのゴーストに送るかを指定します。
//     Sakura名を指定してください。また、__SYSTEM_ALL_GHOST__で全起動中ゴーストに通知します。
//     省略すると「プラグインメニューを実行したゴースト」に送ります
//   res_marker
//     バルーンのマーカー(下に小さく出るステータス表示)に表示する文字を指定できます。
//   res_event_option / res_script_option
//     それぞれ、EventOption / ScriptOptionヘッダを設定します。
// 詳しくはプラグイン仕様書を参照してください。
// http://emily.shillest.net/specwiki/index.php?PLUGIN%2F2.0%2F%E4%BB%95%E6%A7%98%E6%9B%B8

// 初期化処理
OnSystemLoad
{
  name = 'Bouyomi'

  FUNCTIONEX('proxy_ex.dll', '/charset', 'UTF-8')

  // 棒読みちゃんの実行ファイルのパスが指定されていたらそれを実行
  _exec_path_file = 'BouyomiChan_path.txt'
  _exec_path = ''
  _is_loaded = FOPEN(_exec_path_file, 'r')
  if _is_loaded != 0 {
    _exec_path = FREAD(_exec_path_file)
    FCLOSE(_exec_path_file)
  }
  else {
    // ファイルがなかったら作成
    FOPEN(_exec_path_file, 'w')
    FWRITE(_exec_path_file, 'C:\Program Files\BouyomiChan\BouyomiChan.exe')
    FWRITE(_exec_path_file, '棒読みちゃんの実行ファイル(BouyomiChan.exe)があるパスを1行目に書いてください(上記は例です)。')
    FCLOSE(_exec_path_file)
  }
  if _exec_path != '' && FSIZE(_exec_path) != -1 {
    void EXECUTE(GETSETTING('coreinfo.path')+'core.exe', "-b %(_exec_path)")
  }

  updateOnLoad = 0
}

OnSecondChange
{
  // 起動したら１回だけ更新をかける
  if updateOnLoad == 0 {
    updateOnLoad = 1
    "\C\![updateother,--plugin=%(name)]"
  }
}

OnOtherGhostTalk
{
  Speak(reference[4], reference[0])
}

Speak
{
  // _argv[0]: 読み上げるセリフ
  // _argv[1]: セリフ元ゴースト名

  _voice = 0

  void EXECUTE(GETSETTING('coreinfo.path')+'core.exe', "-g %(CHR(34))%(_argv[1])%(CHR(34)) -v %(GetVoiceCode(_argv[1],_voice)) %(CHR(34))%(_argv[0])%(CHR(34))")
}

OnMenuExec
{
  // 選択可能な声質リストを更新
  voicelist = SPLIT(FUNCTIONEX('proxy_ex.dll', 'core.exe', '-l'), CHR(1))

  // 対象ゴーストのキャラクターリスト(\0,\1,...)を取得
  _path = RE_REPLACE(reference[4], '\\$', '')
  _characterlist = SPLIT(FUNCTIONEX('proxy_ex.dll', 'core.exe', '-count', _path), CHR(1))

  _res = "\_q\0\b[2]%(reference[1])\n\n"
  for _i=0; _i<ARRAYSIZE(_characterlist); _i++ {
    _voicename = GetVoiceName(GetVoiceCode(reference[1], _i))
    _res += "%(_characterlist[_i]):\n  \q[【%(_voicename)】,OnMenuShow,%(reference[1]),%(_i),%(_path)]\n\n"
  }
  _res + "\q[✕,]\n\f[align,right]\f[height,10](%(version))\_l[0,0]"
}

OnMenuShow
{
  // reference[0]: 呼び出し元ゴースト名
  // reference[1]: 設定対象スコープ
  // reference[2]: 対象ゴーストのフルパス

  _msg = "\![set,autoscroll,disable]\_q\0\b[2]%(reference[0])\n\\%(reference[1])側のボイスを指定してください\n(現在: %(GetVoiceName(GetVoiceCode(reference[0],reference[1]))))\n\n"

  _selection = ''
  foreach voicelist; _line {
    _sp = SPLIT(_line, ',')
    _selection += "\![*]\q[%(_sp[1]),OnVoiceSelected,%(reference[0]),%(reference[1]),%(_sp[0]),%(_fullpath)]\n"
  }
  _msg + _selection + "\n\q[←,OnMenuExec,dummy,%(reference[0]),dummy,dummy,%(reference[2])] \q[✕,]"
}

GetMD5
{
  // _argv[0]: md5に変換したい文字列

  REPLACE(FUNCTIONEX('proxy_ex.dll', 'core.exe', '-hash', _argv[0]), ' ', '')
}

OnVoiceSelected
{
  // reference[0]: 対象ゴースト名
  // reference[1]: 設定対象スコープ
  // reference[2]: 棒読みちゃんボイス番号
  // reference[3]: 対象ゴーストのフルパス

  _varname = GetMD5(reference[0]+reference[1]) + '.voice'
  LETTONAME(_varname, reference[2])
  SAVEVAR()

  "\![raiseplugin,%(name),OnMenuExec,dummy,%(reference[0]),dummy,dummy,%(reference[3])]"
}

GetVoiceName
{
  // _argv[0]: 棒読みちゃんボイス番号

  foreach voicelist; _line {
    _sp = SPLIT(_line, ',')
    if _sp[0] == _argv[0] {
      _sp[1]
      return
    }
  }

  "【ボイス番号%(_argv[0])は現在存在しません】"
}

GetVoiceCode
{
  // _argv[0]: 対象ゴースト名
  // _argv[1]: 対象スコープ

  _varname = GetMD5(_argv[0]+_argv[1]) + '.voice'
  if ISVAR(_varname) {
    EVAL(_varname)
  }
  else {
    0
  }
}

version
{
  "%(name)/1.4.0"
}
